;; Import the parser and lexer generators.
(require (lib "yacc.ss" "parser-tools")
         (lib "lex.ss" "parser-tools")
         (prefix : (lib "lex-sre.ss" "parser-tools")))

(require (lib "pretty.ss"))

(define-tokens value-tokens (NUM ID))

(define-empty-tokens op-tokens
  (OP
   CP
   COMMA
   EQ1
   LET   
   IN
   +
   -
   *
   /
   EOF))

(define-lex-abbrevs
  (lower-letter (:/ "a" "z"))
  (upper-letter (:/ "A" "Z"))
  (letter (:or lower-letter upper-letter))
  (digit (:/ "0" "9"))
  (ident (:+ letter))  
  (number (:+ digit)))


;get-token: inputPort -> token
(define get-token
  (lexer
   ((eof) 'EOF)
   ("let" 'LET)
   ("in" 'IN)
   ("(" 'OP)
   (")" 'CP)   
   ("," 'COMMA)
   ("=" 'EQ1)
   ("+" '+)
   ("-" '-)
   ("*" '*)
   ("/" '/)
   (number (token-NUM (string->number lexeme)))
   (ident (token-ID (string->symbol lexeme)))
   (whitespace (get-token input-port))))

;;; data definitions


;; A small language expression (SmallLangExp) is one of the following.
;; a number n
;; an identifier x
;; a sum with parts e1 and e2,
;; where e1 and e2 are small language expressions
;; a difference with parts e1 and e2,
;; where e1 and e2 are small language expressions
;; a product with parts e1 and e2,
;; where e1 and e2 are small language expressions
;; a quotient with parts e1 and e2,
;; where e1 and e2 are small language expressions
;; a negation with part e,
;; where e is an small language expression
;; a bindings with parts defs and e,
;; where defs is a list of identifiers * SmallLangExp
;; and e is an small language expression
;; functions for associated with each part: predicate, constructor, selectors.
;; Number is a Scheme number
;; Identifier is a Scheme symbol


;; Constructors

; make-sum: SmallLangExp * SmallLangExp -> SumExp
(define (make-sum exp1 exp2)
  (list 'sum exp1 exp2))

; make-diff: SmallLangExp * SmallLangExp -> DiffExp
(define (make-diff exp1 exp2)
  (list 'diff exp1 exp2))

; make-prod: SmallLangExp * SmallLangExp -> ProdExp
(define (make-prod exp1 exp2)
  (list 'prod exp1 exp2))

; make-quo: SmallLangExp * SmallLangExp -> QuoExp
(define (make-quo exp1 exp2)
  (list 'quo exp1 exp2))

; make-neg: SmallLangExp -> NegExp
(define (make-neg exp)
  (list 'neg exp))

; make-let: Listof(Identifier*SmallLangExp) * SmallLangExp -> BindingExp
; Identifier*SmallLangExp is represented as a two element list
(define (make-let defs exp)
  (list 'with-bindings defs exp))



;; Predicates

; sum?: Any -> Bool
(define (sum? a) (and (pair? a) (eq? (car a) 'sum)))

; difference?: Any -> Bool
(define (difference? a) (and (pair? a) (eq? (car a) 'diff)))

; product?: Any -> Bool
(define (product? a) (and (pair? a) (eq? (car a) 'prod)))

; quotient?: Any -> Bool
(define (quotient? a) (and (pair? a) (eq? (car a) 'quo)))

; negate?: Any -> Bool
(define (negate? a) (and (pair? a) (eq? (car a) 'neg)))

; let?: Any -> Bool
(define (let? a) (and (pair? a) (eq? (car a) 'with-bindings)))



;; Selectors

; arg1:SmallLangExp -> SmallLangExp
(define (arg1 e) (car (cdr e)))

; arg2: SmallLangExp -> SmallLangExp
(define (arg2 e) (car (cdr (cdr e))))

; neg-exp: NegExp -> SmallLangExp
(define (neg-exp e) (car (cdr e)))

; let-defs: LetExp -> SmallLangExp
(define (let-defs e) (car (cdr e)))

; let-exp: LetExp -> SmallLangExp
(define (let-exp e) (car (cdr (cdr e))))

; parse-small-lang: (() -> token) -> SmallLangExp
(define parse-small-lang
  (parser
   (start exp)
   (end EOF)
   (tokens value-tokens op-tokens)
   (error (lambda (a b c) (error 'parse-small-lang "error occurred, ~v ~v ~v" a b c)))
   (grammar
    (exp ((LET let-defs IN exp) (make-let $2 $4))
         ((math-exp) $1))
    (let-def ((ID EQ1 exp) (list $1 $3)))
    (let-defs ((let-def) (list $1))
              ((let-def COMMA let-defs) (cons $1 $3)))
    (math-exp ((math-exp + term) (make-sum $1 $3))
              ((math-exp - term) (make-diff $1 $3))
              ((term) $1))
    (term ((term * factor) (make-prod $1 $3))
          ((term / factor) (make-quo $1 $3))
          ((factor) $1))
    (factor ((ID) $1)
            ((NUM) $1)
            ((- factor) (make-neg $2))
            ((OP exp CP) $2)))))


; lexer/parser test
(let* ((example "let x = -2 + 3 * 4, y = 0 in -2+5*x+y")
       (i (open-input-string example))) ; convert string to inputPort
  (equal? (parse-small-lang (lambda () (get-token i)))
          '(with-bindings ((x (sum (neg 2) (prod 3 4)))
                           (y 0))
             (sum (sum (neg 2) (prod 5 x)) y))))

;;Constructor tests
;(equal? (make-sum 2 3) '(sum 2 3))
;(equal? (make-diff 2 3) '(diff 2 3))
;(equal? (make-prod 2 3) '(prod 2 3))
;(equal? (make-quo 2 3) '(quo 2 3))
;(equal? (make-neg 2) '(neg 2))
;(equal? (make-let (list (list 'x 1) (list 'y 2)) 3) '(with-bindings ((x 1) (y 2)) 3))


;; Predicate tests
(eq? (sum? '(sum 2 3)) #t)
(eq? (sum? '3) #f)
;(eq? (difference? '(diff 2 3)) #t)
;(eq? (sum? '3) #f)
;(eq? (product? '(prod 2 3)) #t)
;(eq? (product? '3) #f)
;(eq? (quotient? '(quo 2 3)) #t)
;(eq? (quotient? '3) #f)
;(eq? (negate? '(neg 2 3)) #t)
;(eq? (negate? '3) #f)
;(eq? (let? '(with-bindings 2 3)) #t)
;(eq? (let? '3) #f)

;; Selector tests                   -- The three tests labeled false expect a false result.
;(= (arg1 (make-sum 2 3)) 2)
;(= (arg1 (make-prod 2 3)) 2)
;(= (arg2 (make-sum 2 3)) 3)
;(= (arg2 (make-prod 2 3)) 3)
;(= (neg-exp (make-neg 2)) 2)
;(equal? (neg-exp (make-neg 2)) 'neg)                                                 ;false
;(equal? (let-defs (make-let (list (list 'x 1) (list 'y 2)) 3)) '((x 1) (y 2)))
;(equal? (let-defs (make-let (list (list 'x 1) (list 'y 2)) 3)) 3)                    ;false
;(equal? (let-exp (make-let (list (list 'x 1) (list 'y 2)) 3)) '((x 1) (y 2)))        ;false
;(equal? (let-exp (make-let (list (list 'x 1) (list 'y 2)) 3)) 3)